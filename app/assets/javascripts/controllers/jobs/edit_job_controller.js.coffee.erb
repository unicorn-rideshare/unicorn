module = angular.module('unicornApp.controllers')

module.controller 'EditJobCtrl', ['$scope', '$location', '$routeParams', '$modal', 'Job', 'Provider', 'companyPreference', 'flashService',
 ($scope, $location, $routeParams, $modal, Job, Provider, companyPreference, flashService) ->

   $scope.showActivity = true

   $scope.supervisors = {}
   $scope.supervisorIds = []

   job = Job.get id: $routeParams.id, include_work_orders: true, include_supervisors: true, ->
     $scope.job = job
     $scope.isCancellable = ['canceled'].indexOf($scope.job.status) == -1
     for supervisor in $scope.job.supervisors
       $scope.supervisors[supervisor.id] = true
     $scope.showActivity = !$scope.providers

   providers = Provider.query company_id: companyPreference.get(), ->
     $scope.providers = providers
     $scope.showActivity = !$scope.job

   $scope.$watchCollection 'supervisors', ->
     if $scope.supervisors
       $scope.supervisorIds = []
       for id, status of $scope.supervisors
         $scope.supervisorIds.push(parseInt(id)) if status

   $scope.submit = ->
     supervisors = []
     for provider in $scope.providers
       supervisors.push(provider) if $scope.supervisorIds.indexOf(provider.id) != -1
     $scope.job.supervisors = supervisors
     promise = $scope.job.$update()
     promise.then ->
       flashService.success('Job was successfully updated')
       $location.path('/jobs')

   $scope.addBlueprint = (job) ->
     $scope.modalInstance = $modal.open
       templateUrl: '<%= asset_path('attachments/new.html') %>'
       controller: 'NewAttachmentModalCtrl'
       size: 'lg'
       resolve:
         attachable: -> job
         attachableUri: -> 'jobs'
         tags: -> 'blueprint'
         onAttachmentCreated: -> (attachment) ->
           flashService.success('Blueprint uploaded successfully')

   $scope.openAttachments = (attachable) ->
     $scope.modalInstance = $modal.open
       templateUrl: '<%= asset_path('attachments.html') %>'
       controller: 'AttachmentsIndexModalCtrl'
       size: 'lg'
       resolve:
         attachable: -> attachable
         attachableUri: -> 'jobs'
         allowAttachmentDeletion: -> false

   $scope.openComments = (commentable) ->
     $scope.modalInstance = $modal.open
       templateUrl: '<%= asset_path('comments.html') %>'
       controller: 'CommentsIndexModalCtrl'
       size: 'lg'
       resolve:
         commentable: -> commentable
         commentableUri: -> 'jobs'

   $scope.cancelJob = ->
     $scope.job.status = 'canceled'
     $scope.job.$update().then ->
       flashService.warning('Job has been cancelled')
       $location.path('/jobs')
]
