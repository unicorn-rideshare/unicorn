module = angular.module('unicornApp.controllers')

module.controller 'WorkOrderCheckinCtrl', ['$scope', '$window', 'WorkOrder', 'facebook', 'websocket',
  ($scope, $window, WorkOrder, facebook, websocket) ->
    $scope.checkin = null
    $scope.checkinMarker = null
    $scope.map = null
    $scope.facebookUserId = null
    $scope.lastFbLoginChangedTimestamp

    $scope.fbLoginChangedInterval = null
    $scope.fbLoginChangedInterval = setInterval $scope.queryForChangeInFacebookLoginStatus, 250

    $scope.onFbUserConnected = (fbUser) ->
      console.log 'fb user connected'
      console.log fbUser

    $scope.onFbUserDisconnected = (fbUser) ->
      console.log 'fb user disconnected'
      console.log fbUser

    $scope.queryForChangeInFacebookLoginStatus = ->
      facebook.refreshLoginStatus($scope.onFbUserConnected, $scope.onFbUserDisconnected) if $window.fbLoginChangedTimestamp != $scope.lastFbLoginChangedTimestamp
      $scope.lastFbLoginChangedTimestamp = $window.fbLoginChangedTimestamp

    $scope.$watch 'workOrder', (newValue, oldValue) ->
      $scope.workOrder = new WorkOrder(newValue) if newValue && !oldValue

      if $scope.workOrder
        fn = -> $scope.renderMap()
        setTimeout(fn, 25)

        workOrderProvider = $scope.workOrder.work_order_providers[0]
        $scope.provider = workOrderProvider.provider if workOrderProvider
        if $scope.provider
          $scope.checkin = $scope.provider.last_checkin if !$scope.checkin && $scope.provider
          onCheckinReceived = (checkin) ->
            $scope.checkin = checkin
            $scope.renderProviderLocation()
            $scope.zoomToBestFit()
          channel = 'user_checkins_' + $scope.provider.user_id
          websocket.bind(channel, 'new', onCheckinReceived)

    $scope.$on 'work_order_changed', (event, workOrder) ->
      $scope.workOrder = new WorkOrder(workOrder)

    $scope.createCheckinMarker = ->
      if ($scope.checkinMarker)
        $scope.checkinMarker.setMap(null)

      image =
        url: $scope.provider.profile_image_url
        scaledSize: new google.maps.Size(50, 50)
        origin: new google.maps.Point(0, 0)

      $scope.checkinMarker = new google.maps.Marker
        position: $scope.providerLatLng(),
        map: $scope.map,
        title: $scope.provider.user.name,
        icon: image

    $scope.providerLatLng = ->
      checkin = $scope.checkin || (if $scope.provider then $scope.provider.last_checkin else null)
      return $scope.map.getCenter() unless checkin
      new google.maps.LatLng(checkin.latitude, checkin.longitude)

    $scope.zoomToBestFit = ->
      return unless $scope.workOrder
      centerLatitude = $scope.workOrder.customer.contact.latitude
      centerLongitude = $scope.workOrder.customer.contact.longitude
      centerLatLng = new google.maps.LatLng(centerLatitude, centerLongitude)
      $scope.map.setCenter(centerLatLng)

      bounds = new google.maps.LatLngBounds()
      workOrderLatLng = $scope.workOrderLatLng()
      providerLatLng = $scope.providerLatLng()
      bounds.extend(providerLatLng) if providerLatLng
      bounds.extend(workOrderLatLng) if workOrderLatLng
      $scope.map.fitBounds(bounds)

    $scope.renderMap = ->
      unless $scope.map
        defaultOptions =
          zoom: 9

        canvas = document.getElementById($scope.elementId)
        $scope.map = new google.maps.Map(canvas, defaultOptions) if canvas

      if $scope.workOrder
        $scope.createCustomerMarker()
        $scope.renderProviderLocation() if $scope.checkin
        $scope.zoomToBestFit()

    $scope.workOrderLatLng = ->
      return null unless $scope.workOrder
      customerLatitude = $scope.workOrder.customer.contact.latitude
      customerLongitude = $scope.workOrder.customer.contact.longitude
      new google.maps.LatLng(customerLatitude, customerLongitude);

    $scope.createCustomerMarker = ->
      new google.maps.Marker(
        position: $scope.workOrderLatLng(),
        map: $scope.map,
        title: $scope.workOrder.customer.contact.name,
        icon: '<%= asset_path('unicorn-icon-nav.png') %>'
      )

    $scope.renderProviderLocation = ->
      if ($scope.providerMarker)
        $scope.providerMarker.setMap(null)

      profileImageUrl = $scope.provider.profile_image_url || '<%= asset_path('profile-photo.png') %>'

      image =
        url: profileImageUrl
        scaledSize: new google.maps.Size(30, 30)
        origin: new google.maps.Point(0, 0)

      latitude = $scope.checkin.latitude
      longitude = $scope.checkin.longitude
      latLng = new google.maps.LatLng(latitude, longitude)

      $scope.providerMarker = new google.maps.Marker
        position: latLng
        map: $scope.map
        title: $scope.provider.contact.name
        icon: image
]
