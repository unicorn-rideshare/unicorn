module = angular.module('unicornApp.controllers')

module.controller 'JobMapCtrl', [
  '$scope', '$controller', 'Job', 'websocket',
  ($scope, $controller, Job, websocket) ->

    $scope.$watch 'job', (newValue, oldValue) ->
      if newValue
        fn = -> $scope.renderMap()
        setTimeout(fn, 25)

    $scope.mapClicked = (event) ->
      event.preventDefault()
      event.stopPropagation()

      $scope.openInfowindow.close() if $scope.openInfowindow

    $scope.customerLatLng = ->
      latitude= $scope.job.customer.contact.latitude
      longitude = $scope.job.customer.contact.longitude
      new google.maps.LatLng(latitude, longitude)

    $scope.zoomToBestFit = ->
      bounds = new google.maps.LatLngBounds()

      customerLatLng = $scope.customerLatLng()
      bounds.extend(customerLatLng) if customerLatLng

      if $scope.coordinates.length > 0
        for latLng in $scope.coordinates
          bounds.extend(latLng)

      $scope.map.fitBounds(bounds)

    $scope.renderMap = ->
      unless $scope.map
        centerLatLng = $scope.customerLatLng()

        defaultOptions =
          center: centerLatLng
          zoom: 18

        canvas = document.getElementById($scope.elementId)
        $scope.map = new google.maps.Map(canvas, defaultOptions) if canvas

        $scope.coordinates = [centerLatLng]

        $scope.renderCustomerLocation()
        $scope.zoomToBestFit()
        $scope.renderTraffic()

    $scope.renderCustomerLocation = ->
      if $scope.originMarker
        $scope.originMarker.setMap(null)
        delete $scope.originMarker

      latitude = $scope.job.customer.contact.latitude
      longitude = $scope.job.customer.contact.longitude
      title = $scope.job.customer.name
      icon =
        url: '<%= asset_path('map-pin.png') %>'
        scaledSize: new google.maps.Size(38, 42)
        origin: new google.maps.Point(0, 0)
      $scope.createMarker(latitude, longitude, title, icon)

    $scope.renderTraffic = ->
      trafficLayer = new google.maps.TrafficLayer()
      trafficLayer.setMap($scope.map)

    $scope.createMarker = (latitude, longitude, title, icon) ->
      latLng = new google.maps.LatLng(latitude, longitude)
      $scope.coordinates.push(latLng)
      new google.maps.Marker(
        position: latLng,
        map: $scope.map,
        title: title,
        icon: icon
      )
]
